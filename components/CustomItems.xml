<?xml version="1.0" encoding="utf-8" ?>

<component name="TwoColumnCustomItemExample" extends="StandardDialog" initialFocus="buttonArea" >

<script type="text/brightscript" >
<![CDATA[
function init()
    print "Creating CustomItems"

    ' the left column will be set to 2/3 of the total custom item
    ' width in the custom item's width field observer
    m.leftColumnPercent = 0.67

    m.subscriptiontListItem = m.top.findNode("subscriptiontListItem")
    m.subscriptiontListItem.observeField("width", "subscriptiontListItemWidthChanged")
    m.subscriptiontList = m.top.findNode("subscriptiontList")

    addSubscriptionOption("One Week", "Free")
    addSubscriptionOption("One Month", "$2")
    addSubscriptionOption("One Year",  "$20")

    m.buttonArea = m.top.findNode("buttonArea")
    m.top.observeFieldScoped("buttonSelected", "printSelectedButtonAndClose")
end function

sub addSubscriptionOption(time as string, price as string)
    optLayout = m.subscriptiontList.createChild("LayoutGroup")
    optLayout.layoutDirection = "horiz"
    timeLabel = optLayout.createChild("Label")
    timeLabel.text = time
    timeLabel.color = "0x121212FF"
    priceLabel = optLayout.createChild("Label")
    priceLabel.text = price
    priceLabel.color = "0x121212FF"
end sub

sub subscriptiontListItemWidthChanged()
    numItems = m.subscriptiontList.getChildCount()
    for i = 0 to numItems - 1
        item = m.subscriptiontList.getChild(i)
        timeLabel = item.getChild(0)
        timeLabel.width = m.leftColumnPercent * m.subscriptiontListItem.width
	? "set width of timeLabel "; timeLabel.text; " to "; timeLabel.width
    end for
end sub

sub printSelectedButtonAndClose()
    print "m.buttonArea button ";m.buttonArea.getChild(m.top.buttonSelected).text;" selected"
    m.top.close = true
end sub

]]>
</script>

<children>
    <StdDlgTitleArea primaryTitle="StdDlgCustomItem: Two Columns" />
    <StdDlgContentArea>
        <StdDlgTextItem text="Subscription options" />
        <StdDlgCustomItem id="subscriptiontListItem">
            <LayoutGroup id="subscriptiontList" layoutDirection="vert" itemSpacings="[20]">
            </LayoutGroup>
        </StdDlgCustomItem>
    </StdDlgContentArea>
    <StdDlgButtonArea id="buttonArea">
        <StdDlgButton text="OK" />
    </StdDlgButtonArea>
</children>

</component>
